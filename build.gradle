plugins {
    id("org.openstreetmap.josm").version("0.7.1")
    id 'java'
    id 'eclipse'
    id 'idea'
}

// For the dependencies of our project (not of this build script itself), we also need the
// local Maven cache. We use it to get the MATSim snapshot.
repositories {
    maven {
        url("https://dl.bintray.com/matsim/matsim")
        content {
            includeGroup("org.matsim")
            includeGroup("org.matsim.contrib")
        }
    }
    maven {
        url("https://artifacts.unidata.ucar.edu/repository/unidata-all")
        content {
            includeGroup("edu.ucar")
        }
    }
    maven {
        url("http://nexus.onebusaway.org/nexus/content/repositories/public/")
        content {
            includeGroup("com.conveyal")
        }
    }
    jcenter()
}

// Use the `packIntoJar` configuration from gradle-josm-plugin for our external dependency (MATSim), whose classes get zipped into our jar.
configurations.packIntoJar {
    exclude(group: 'org.geotools')
    exclude(group: 'org.jfree')
    exclude(group: 'commons-logging')
    exclude(group: 'commons-io')
    exclude(group: 'org.apache.httpcomponents')
}

dependencies {
    def openjfxClasspath = System.getenv("OPENJFX_CLASSPATH")
    if (openjfxClasspath != null) {
        implementation(files(openjfxClasspath))
    }
    packIntoJar ('org.matsim:matsim:12.0') {changing = true}
    packIntoJar ('org.matsim.contrib:otfvis:12.0') {changing = true}
    packIntoJar ('org.matsim.contrib:emissions:12.0') {changing = true}
    packIntoJar ('com.conveyal:gtfs-lib:5.0.7')

    def junitVersion = "5.5.2"
    testImplementation ("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly ("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation ("org.junit.vintage:junit-vintage-engine:$junitVersion")
    testImplementation ('org.openstreetmap.josm:josm-unittest:SNAPSHOT') {changing = true}
    testImplementation ("com.github.tomakehurst:wiremock:2.25.1")
    testImplementation ("org.awaitility:awaitility:4.0.1")
}

sourceCompatibility = 11
archivesBaseName = "matsim"
josm {
    josmCompileVersion = 17428
    manifest {
        author = "Nico Kuehnel"
        description = "Allows to edit and extract network information for the traffic simulation MATSim"
        iconPath = "images/dialogs/matsim-scenario.png"
        minJosmVersion = 17023
        mainClass = "org.matsim.contrib.josm.MATSimPlugin"
        pluginDependencies << 'jts' << 'javafx' << 'geotools'
        website = new URL("http://www.matsim.org")
        oldVersionDownloadLink 17023, 'v1.0.7', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v1.0.7/matsim.jar')
        oldVersionDownloadLink 16392, 'v1.0.6', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v1.0.6/matsim.jar')
        oldVersionDownloadLink 16239, 'v1.0.5', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v1.0.5/matsim.jar')
        oldVersionDownloadLink 15007, 'v1.0.3', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v1.0.3/matsim.jar')
        oldVersionDownloadLink 14418, 'v1.0.0', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v1.0.0/matsim.jar')
        oldVersionDownloadLink 14382, 'v0.9.8', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.8/matsim.jar')
        oldVersionDownloadLink 14289, 'v0.9.6', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.6/matsim.jar')
        oldVersionDownloadLink 13922, 'v0.9.5', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.5/matsim.jar')
        oldVersionDownloadLink 13878, 'v0.9.2', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.2/matsim.jar')
        oldVersionDownloadLink 12881, 'v0.8.8', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.8.8/matsim.jar')
        oldVersionDownloadLink 12450, 'v0.7.9', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.7.9/matsim.jar')
        oldVersionDownloadLink 10340, 'v0.7.5', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.7.5/matsim.jar')
        oldVersionDownloadLink  9278, 'v0.5.9', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.5.9/matsim.jar')
    }
    i18n {
      pathTransformer = getPathTransformer(project.projectDir, "github.com/matsim-org/josm-matsim-plugin/blob")
    }
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    "-Xlint:deprecation"
  ]
}

tasks.test.outputs.dir("$buildDir/test-output")

test {
  testLogging {
    exceptionFormat "full"
    events "skipped", "failed"
  }
}
