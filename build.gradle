plugins {
    id 'org.openstreetmap.josm' version '0.5.0'
    id 'java'
    id 'eclipse'
    id 'idea'
}

// For the dependencies of our project (not of this build script itself), we also need the
// local Maven cache. We use it to get the MATSim snapshot.
repositories {
    maven {
        url 'http://dl.bintray.com/matsim/matsim'
    }
    maven {
        url 'http://download.osgeo.org/webdav/geotools'
    }
    maven {
        url 'https://oss.jfrog.org/artifactory/libs-snapshot'
    }
    maven {
        url 'https://jitpack.io'
    }
}

// Use the `packIntoJar` configuration from gradle-josm-plugin for our external dependency (MATSim), whose classes get zipped into our jar.
configurations {
    packIntoJar.exclude(group: 'org.geotools')
    packIntoJar.exclude(group: 'kml')
    packIntoJar.exclude(group: 'org.jfree')
    packIntoJar.exclude(group: 'commons-logging')
    packIntoJar.exclude(group: 'commons-io')
    packIntoJar.exclude(group: 'org.apache.httpcomponents')
}

dependencies {
    packIntoJar ('org.matsim:matsim:0.11.0-SNAPSHOT') {changing = true}
    packIntoJar ('org.matsim.contrib:otfvis:0.11.0-SNAPSHOT') {changing = true}
    packIntoJar ('org.matsim.contrib:emissions:0.11.0-SNAPSHOT') {changing = true}
    packIntoJar ('com.github.conveyal:gtfs-lib:v1.1.0')

    def junitVersion = "5.2.0"
    testImplementation ("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly ("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation ("org.junit.vintage:junit-vintage-engine:$junitVersion")
    testImplementation ('org.openstreetmap.josm:josm-unittest:') {changing = true}
    testImplementation ("com.github.tomakehurst:wiremock:2.18.0")
    testImplementation ("org.awaitility:awaitility:3.1.2")
}

sourceCompatibility = 1.8
archivesBaseName = "matsim"
josm {
    josmCompileVersion = 14382
    manifest {
        author = "Nico Kuehnel"
        description = "Allows to edit and extract network information for the traffic simulation MATSim"
        iconPath = "images/dialogs/matsim-scenario.png"
        minJosmVersion = 14382
        mainClass = "org.matsim.contrib.josm.MATSimPlugin"
        pluginDependencies << 'jts' << 'geotools'
        website = new URL("http://www.matsim.org")
        oldVersionDownloadLink 14289, 'v0.9.6', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.6/matsim.jar')
        oldVersionDownloadLink 13922, 'v0.9.5', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.5/matsim.jar')
        oldVersionDownloadLink 13878, 'v0.9.2', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.9.2/matsim.jar')
        oldVersionDownloadLink 12881, 'v0.8.8', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.8.8/matsim.jar')
        oldVersionDownloadLink 12450, 'v0.7.9', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.7.9/matsim.jar')
        oldVersionDownloadLink 10340, 'v0.7.5', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.7.5/matsim.jar')
        oldVersionDownloadLink  9278, 'v0.5.9', new URL('https://github.com/matsim-org/josm-matsim-plugin/releases/download/v0.5.9/matsim.jar')
    }
    i18n {
      pathTransformer = getPathTransformer("github.com/matsim-org/josm-matsim-plugin/blob")
    }
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    "-Xlint:deprecation"
  ]
}

tasks.test.outputs.dir("$buildDir/test-output")

test {
  testLogging {
    exceptionFormat "full"
    events "skipped", "failed"
  }
}
